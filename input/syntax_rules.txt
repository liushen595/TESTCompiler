<program>                   { <declaration_list> <statement_list> }
<declaration_list>          <declaration_stat> <declaration_list> | ε
<declaration_stat>          int IDENTIFIER ;
<statement_list>            <statement> <statement_list> | ε
<statement>                 <if_stat> | <other_stat>
<if_stat>                   if ( <expression> ) <statement> <else_part>
<else_part>                 else <statement> | ε
<other_stat>                <while_stat> | <for_stat> | <write_stat> | <read_stat> | <compound_stat> | <expression_stat>
<while_stat>                while ( <expression> ) <statement>
<for_stat>                  for ( <expression> ; <expression> ; <expression> ) <statement>
<write_stat>                write <expression> ;
<read_stat>                 read IDENTIFIER ;
<compound_stat>             { <statement_list> }
<expression_stat>           <expression> ; | ;
<expression>                <additive_expr> <expression_prime>
<expression_prime>          <rel_op> <additive_expr> | = <expression> | ε
<rel_op>                    > | < | >= | <= | == | !=
<additive_expr>             <term> <additive_expr_prime>
<additive_expr_prime>       + <term> <additive_expr_prime> | - <term> <additive_expr_prime> | ε
<term>                      <factor> <term_prime>
<term_prime>                * <factor> <term_prime> | / <factor> <term_prime> | ε
<factor>                    ( <expression> ) | IDENTIFIER | NUMBER